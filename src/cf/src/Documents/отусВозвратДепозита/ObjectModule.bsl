#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Контроль суммы депозита
	СуммаДепозита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказКлиента, "СуммаДепозита");
	Если СуммаДокумента > СуммаДепозита Тогда
       	ТекстСообщения = СтрШаблон(НСтр("ru = 'Превышение суммы депозита %1 по заказу климента %2'"), СуммаДепозита, ЗаказКлиента);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
		ЗаписьЖурналаРегистрации("Ошибка",, Метаданные.Документы.отусВозвратДепозита, Ссылка, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	// Контроль полученной оплаты от клиента
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	отусВзаиморасчетыСКлиентамиОстатки.ЗаказКлиента КАК ЗаказКлиента,
	|	отусВзаиморасчетыСКлиентамиОстатки.Организация КАК Организация,
	|	&СуммаДокумента - отусВзаиморасчетыСКлиентамиОстатки.СуммаОстаток КАК СуммаНехватки
	|ИЗ
	|	РегистрНакопления.отусВзаиморасчетыСКлиентами.Остатки КАК отусВзаиморасчетыСКлиентамиОстатки
	|ГДЕ
	|	отусВзаиморасчетыСКлиентамиОстатки.ЗаказКлиента = &ЗаказКлиента
	|	И отусВзаиморасчетыСКлиентамиОстатки.Организация = &Организация
	|	И отусВзаиморасчетыСКлиентамиОстатки.СуммаОстаток < &СуммаДокумента";
	
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СуммаДокумента", СуммаДокумента);
			
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
       	ТекстСообщения = СтрШаблон(НСтр("ru = 'Превышение задолженности перед клиентом %1 по заказу %2 на %3 руб.'"), Клиент, ЗаказКлиента, Выборка.СуммаНехватки);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
		ЗаписьЖурналаРегистрации("Ошибка",, Метаданные.Документы.отусВозвратДепозита, Ссылка, ТекстСообщения);
		Возврат;
	КонецЕсли;	
	
	// Движения
	
	// Взаиморасчеты с клиентами	
	НаборДвижений = Движения.отусВзаиморасчетыСКлиентами;
	НаборДвижений.Записывать = Истина;
	
	Движение = НаборДвижений.ДобавитьРасход();
	Движение.Период			= Дата;
	Движение.Организация	= Организация;
	Движение.Клиент 		= Клиент;
	Движение.ЗаказКлиента 	= ЗаказКлиента;
	Движение.Сумма 			= СуммаДокумента;
	
	Движения.Записать();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.отусЗаказКлиента") Тогда
		
		ЗаказКлиента = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДепозита;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Организация,Клиент");
				
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
	
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли